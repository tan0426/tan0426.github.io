---
layout: default
title: 10. 찾아서 볼 WRITE 표 그리기
parent: abapstudy
nav_order: 10
---
# 10. 찾아서 볼 WRITE 표 그리기

```abap
TABLES : ZDSUWONT02.

DATA : GT_ZDSUWONT02 LIKE TABLE OF ZDSUWONT02 WITH HEADER LINE.

SELECT-OPTIONS : S_ZSDATE FOR ZDSUWONT02-ZSDATE,
                 S_ZEMPNO FOR ZDSUWONT02-ZEMPNO,
                 S_ZEMPNM FOR ZDSUWONT02-ZEMPNM,
                 S_ZDEPT FOR ZDSUWONT02-ZDEPT,
                 S_ZGEN FOR ZDSUWONT02-ZGENDER.

INITIALIZATION.

TOP-OF-PAGE. "페이지 제목 OR 값들의 헤딩
WRITE : /20(20) SY-ULINE.
WRITE : /20(1) SY-VLINE NO-GAP.
FORMAT COLOR 5 ON. "1~5까지 색깔 지정되어 있음.
WRITE : 21(18)'부양 가족 수' NO-GAP CENTERED.
FORMAT COLOR OFF.
WRITE : 39(1) SY-VLINE NO-GAP.
WRITE : /20(20) SY-ULINE.

"헤딩
WRITE : /1(60) SY-ULINE.
WRITE : /1(1) SY-VLINE NO-GAP.
WRITE : 2(18) '사원 번호' NO-GAP CENTERED.
WRITE : 20(1) SY-VLINE NO-GAP.
WRITE : 21(18) '사원 명' NO-GAP CENTERED.
WRITE : 39(1) SY-VLINE NO-GAP.
WRITE : 40(20) '부양 가족 수' NO-GAP CENTERED.
WRITE : 60(1) SY-VLINE NO-GAP.
WRITE : /1(60) SY-ULINE.

START-OF-SELECTION.

SELECT *
  INTO CORRESPONDING FIELDS OF TABLE GT_ZDSUWONT02
  FROM ZDSUWONT02
  WHERE ZSDATE IN S_ZSDATE
  AND ZEMPNO IN S_ZEMPNO
  AND ZEMPNM IN S_ZEMPNM
  AND ZDEPT IN S_ZDEPT
  AND ZGENDER IN S_ZGEN.

SORT GT_ZDSUWONT02.
LOOP AT GT_ZDSUWONT02.

*  WRITE : /1(60) SY-ULINE.
  WRITE : /1(1) SY-VLINE NO-GAP.
  IF GT_ZDSUWONT02-ZFAMNO >= 3.
    FORMAT COLOR 3 ON.
    WRITE : 2(18) GT_ZDSUWONT02-ZEMPNO NO-GAP CENTERED.
    FORMAT COLOR OFF.
  ELSE.
    WRITE : 2(18) GT_ZDSUWONT02-ZEMPNO NO-GAP CENTERED.
  ENDIF.
  WRITE : 20(1) SY-VLINE NO-GAP.
  WRITE : 21(18) GT_ZDSUWONT02-ZEMPNM NO-GAP CENTERED.
  WRITE : 39(1) SY-VLINE NO-GAP.
  WRITE : 40(20) GT_ZDSUWONT02-ZFAMNO NO-GAP CENTERED.
  WRITE : 60(1) SY-VLINE NO-GAP.
  WRITE : /1(60) SY-ULINE.

  AT LAST.
    SUM.
*    WRITE : /1(60) SY-ULINE.
    WRITE : /1(1) SY-VLINE NO-GAP.
    FORMAT COLOR 4 ON.
    WRITE : 2(37) '합 계' NO-GAP CENTERED.
    WRITE : 39(1) SY-VLINE NO-GAP.
    WRITE : 40(20) GT_ZDSUWONT02-ZFAMNO NO-GAP CENTERED.
    FORMAT COLOR OFF.
    WRITE : 60(1) SY-VLINE NO-GAP.
    WRITE : /1(60) SY-ULINE.
  ENDAT.

*
*  WRITE : / GT_ZDSUWONT02-ZEMPNO,
*            GT_ZDSUWONT02-ZEMPNM,
*            GT_ZDSUWONT02-ZFAMNO.

ENDLOOP.
```

## WRITE MODULARIZATION

WRITE 할 INTERNAL TABLE을 만든 후 FIELD의 정보를 담을 INTERNAL TABLE을 만든다.

그리고 VLINE, ULINE, FIELD의 POSITION과 LENGTH들을 담을 변수들을 만들어준다.

```abap
*&---------------------------------------------------------------------*
*&  Include           ZCOTJ_03_TOP
*&---------------------------------------------------------------------*

DATA : GS_DATA LIKE SCARR,
       GT_DATA LIKE TABLE OF GS_DATA.

DATA : BEGIN OF GS_FIELD,
        FIELDNAME(10),
        FIELDLENG TYPE I,
       END OF GS_FIELD,
       GT_FIELD LIKE TABLE OF GS_FIELD.

DATA : GV_FNAME(30).

DATA : F_POSI TYPE I,
       F_LENG TYPE I,
       F_NUM TYPE I.

DATA : UL_POSI TYPE I,
       VL_POSI TYPE I,
       UL_LENG TYPE I,
       VL_LENG TYPE I.
```

WRITE문은 제목과 헤더라인을 우선 TOP-OF-PAGE에 배치하고, 그 아래에 데이터를 넣는다.

```abap
INCLUDE ZCOTJ_03_TOP.
INCLUDE ZCOTJ_03_SCR.
INCLUDE ZCOTJ_03_CLS.
INCLUDE ZCOTJ_03_PBO.
INCLUDE ZCOTJ_03_PAI.
INCLUDE ZCOTJ_03_F01.

INITIALIZATION.
  PERFORM SELECT_DATA.
TOP-OF-PAGE.
  PERFORM WRITE_TOP_OF_PAGE.
  PERFORM FIELD_DATA.
  PERFORM WRITE_HEAD.

START-OF-SELECTION.
  PERFORM WRITE.
```

우선 WRITE할 TABLE의 FIELD정보들을 필드카탈로그처럼 매크로로 모듈화 해서 입력한다.

```abap
FORM FIELD_DATA.
  DEFINE _FIELDNAME_MAC.
    GS_FIELD-FIELDNAME = &1.
    GS_FIELD-FIELDLENG = &2.
    APPEND GS_FIELD TO GT_FIELD.
    CLEAR GS_FIELD.
  END-OF-DEFINITION.

  _FIELDNAME_MAC 'CARRID'   '10'.
  _FIELDNAME_MAC 'CARRNAME' '15'.
  _FIELDNAME_MAC 'CURRCODE' '10'.
  _FIELDNAME_MAC 'URL'      '30'.

  F_NUM = LINES( GT_FIELD ). "필드 수
ENDFORM.                    " FIELD_DATA
```

TOP-OF-PAGE는 하나뿐이므로 그냥 하드코딩 해 두었다.

```abap
FORM WRITE_TOP_OF_PAGE.
  WRITE : /20(20) SY-ULINE,
          /20(1) SY-VLINE NO-GAP.
  FORMAT COLOR 4 ON.
  WRITE : 21(18)'SCARR TABLE WRITE MODULARIZATION' NO-GAP CENTERED.
  FORMAT COLOR OFF.
  WRITE : 39(1) SY-VLINE NO-GAP.
  WRITE : /20(20) SY-ULINE.
ENDFORM.                    " WRITE
```

헤더라인을 만들어준다.

ULINE, VLINE의 POSITION과 VLINE LENGTH를 지정해주고, ULINE은 필드들의 길이와 갯수에따라 다르므로 이를 계산해준다.

```abap
FORM SET_POSITION .
  CLEAR : UL_POSI, VL_POSI, VL_LENG.

  UL_POSI = 1.
  VL_POSI = 1.
  VL_LENG = 1.
ENDFORM.                    " SET_POSITION
```

```abap
FORM UL_LENG_CAL .
  LOOP AT GT_FIELD INTO GS_FIELD.
    UL_LENG = 1 + GS_FIELD-FIELDLENG + UL_LENG.
  ENDLOOP.

  UL_LENG = UL_LENG + 1.
ENDFORM.                    " ULINE_CAL
```

그다음 헤더라인을 만든다.

```abap
FORM WRITE_HEAD .
  PERFORM SET_POSITION. "FIELD_LENGTH, FIELD_NUM
  PERFORM UL_LENG_CAL.

  WRITE : /UL_POSI(UL_LENG) SY-ULINE,
          /VL_POSI(VL_LENG) SY-VLINE NO-GAP.

  FORMAT COLOR 3 ON.
  LOOP AT GT_FIELD INTO GS_FIELD.
    F_LENG = GS_FIELD-FIELDLENG.

    F_POSI = VL_POSI + VL_LENG.
    WRITE AT F_POSI(F_LENG) GS_FIELD-FIELDNAME NO-GAP CENTERED.

    VL_POSI = F_POSI + F_LENG.
    WRITE AT VL_POSI(VL_LENG) SY-VLINE NO-GAP.
  ENDLOOP.
  FORMAT COLOR OFF.

  WRITE : /UL_POSI(UL_LENG) SY-ULINE.
ENDFORM.                    " WRITE_HEAD
```

여기까지 TOP-OF-PAGE를 지정했으면, 그 아래 데이터를 WRITE 해 준다.

```abap
FORM SELECT_DATA .
  SELECT *
  INTO CORRESPONDING FIELDS OF TABLE GT_DATA
  FROM SCARR.

  SORT GT_DATA.
ENDFORM.                    " SELECT_DATA
```

```abap
FORM WRITE .
  FIELD-SYMBOLS : <FS> TYPE ANY.

  LOOP AT GT_DATA INTO GS_DATA.
    WRITE : /VL_POSI(VL_LENG) SY-VLINE NO-GAP.

    PERFORM SET_POSITION.

    LOOP AT GT_FIELD INTO GS_FIELD.
      CONCATENATE 'GS_DATA-' GS_FIELD-FIELDNAME INTO GV_FNAME.
      ASSIGN (GV_FNAME) TO <FS>.

      PERFORM SET_POSITION2.

      WRITE AT : F_POSI(F_LENG) <FS> NO-GAP CENTERED.
      WRITE AT : VL_POSI(VL_LENG) SY-VLINE NO-GAP.
    ENDLOOP.
    WRITE AT : /UL_POSI(UL_LENG) SY-ULINE NO-GAP.
    CLEAR : VL_POSI, F_POSI.
  ENDLOOP.
ENDFORM.                    " WRITE
```

